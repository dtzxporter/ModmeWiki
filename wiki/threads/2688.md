# ABNORMAL202 SCRIPTING TUTORIAL 7: WHILE LOOPS
Game Modding | Call of Duty: Black Ops 3 | Scripting

---
<strong style="font-size: 1.4em;"><span style="text-decoration: underline;text-decoration-color: #34a7f9;"><span style="color:#34a7f9;">Abnormal202</span></span>:</strong>

<p><span style="color:#00ffff;">ABNORMAL202 SCRIPTING TUTORIAL 7: WHILE LOOPS</span><br /> <br /><a href="https://forum.modme.co/threads/abnormal202-scripting-tutorials-master.2680/">See ALL Tutorials</a><br /> <br /><span style="color:#00ffff;">DESCRIPTION</span><br /> <br /><span style="color:#ffcc00;">While Loops</span> are the first type of loops we&#39;ll be looking at. Loops essentially allow you to repeat the same code over &amp; over again multiple times, or even an infinite amount of time so long as the game is running.<br /> <br />I will also talk about <span style="color:#ffcc00;">notifies</span>, <span style="color:#ffcc00;">endons</span>, and <span style="color:#ffcc00;">waittills</span>, which are all a form of loop manipulation, <strong>however they can be used outside of loops as well.</strong><br /> <br /><span style="color:#00ffff;">CREATING A WHILE LOOP</span><br /> <br />A <span style="color:#ffcc00;">while loop</span> looks like this:<br /> <br />{% highlight php %}
while(condition)
{
    IPrintLnBold("Hello");
    wait(0.05);
}{% endhighlight %}
 <br />notice:<br /><ul><li>the word <span style="color:#ffcc00;">while</span><br /><br /><li>the <span style="color:#ffcc00;">()</span><br /><br /><li>the condition. You insert a boolean in between the <span style="color:#ffcc00;">()</span>, so just like in an if statement it will check if it&#39;s true or false. If the condition is true the loop continues; if it is false the loop stops and the code below it is run.<br /><br /><li>the <span style="color:#ffcc00;">{}</span>. Similar to a <span style="color:#ffcc00;">function</span> or <span style="color:#ffcc00;">if statement</span>, you should put all code you want to be in the while in between these two <span style="color:#ffcc00;">{}</span>.<br /><br /><li>the <span style="color:#ffcc00;">wait</span>. <strong>ALL WHILE LOOPS NEED A WAIT OR WAITTILL INSIDE THEM.</strong> The smallest amount of time you can wait is 0.05 seconds, which is often used in while loops. If you forget to put a wait inside your while loop, launcher will <strong>not</strong> give you an error. Instead you will find that in-game when the code begins to read the while loop without a wait, the game will freeze and give a &quot;Connection Interrupted&quot; error, and you will have to force quit to get out usually.<br /><br /></li></li></li></li></li></ul>So how can we use While Loops? We can use them to repeat code over and over as long as the condition inside the <span style="color:#ffcc00;">()</span> remains true. Or if we just want it to always repeat, we can literally input <span style="color:#ffcc00;">true </span>into the condition, or<span style="color:#ffcc00;">1</span>, since as we remember 1 = true, 0 = false:<br /> <br />{% highlight php %}
while(1)
{
    IPrintLnBold("Are we there yet?");
    wait(2);
}{% endhighlight %}
 <br /> <br />In this example, the line &quot;Are we there yet?&quot; will be printed to the screen every 2 seconds once this while loop is initiated, and will never stop. Just like those damn children.<br /> <br />Perhaps we don&#39;t want it go infinitely though? Here&#39;s an example of how one can use a While Loop for a set amount of times:<br /> <br />{% highlight php %}
times = 0;
while(times &lt; 10)
{
    if(times == 1)
    {
        IPrintLnBold(times + " minute has passed"); //account for pluralization
    }
    else
    {
        IPrintLnBold(times + " minutes have passed"); 
    }

    times ++;
    wait(60);
}{% endhighlight %}
 <br /> <br />In the above case the code will run 10 times before it stops, and the code below it can begin. It should be noted that this specific example could probably be better done using a <span style="color:#ffcc00;">for loop</span>, but we&#39;ll talk about that later.<br /> <br /> <br /><span style="color:#00ffff;">NOTIFIES</span><br /> <br /><span style="color:#ffcc00;">Notifies</span> send, well, a notify on a specific entity to the when they are used. These notifies are picked up by endons and waitills, which I will talk about in a moment.<br /> <br />A standard notify might look like this:<br /> <br />{% highlight php %}
steve notify("stop_eating_my_sandwich");{% endhighlight %}
 <br /> <br />assuming steve is an already defined variable.<br /> <br />notice: <br /><ul><li>the word <span style="color:#ffcc00;">notify</span><br /><br /><li>the <span style="color:#ffcc00;">()</span><br /><br /><li>the <span style="color:#ffcc00;">string</span> inside the <span style="color:#ffcc00;">()</span>. This string should be unique, as it specifies what you are notifying the entity of, so the correct endons and waitills who share that string will go off<br /><br /></li></li></li></ul>just like calling functions, notifies can also pass arguments, to be picked up by waittills. For example:<br /> <br />{% highlight php %}
door = GetEnt("specific_magical_door","targetname");

level notify("magic_door_opened", door, (0,0,50));{% endhighlight %}
 <br /> <br />notice how you can also use that <span style="color:#ff00ff;">level</span> variable on notifies, waittills, and endons.<br /> <br />There are a lot of very useful Treyarch notifies that you can use in your code, that notify when a specific event occurs:<br /> <br />{% highlight php %}
level notify( "end_of_round" ); //used when round ends
level notify( "start_of_round" ); //used when next round starts
level notify( "all_players_connected" ); //used when all players have connected into the game
player notify( "bled_out" ); //used when a player bleeds out, on a specific player
player notify( "player_revived" ); //used when a player is revived, on the revived player
entity notify("death"); //used when some character, such as a player or zombie, dies
entity notify("movedone"); //used when an entity is done moving after being give some sort of move command
player notify("weapon_fired", weapon); //used when a player fires a weapon. also passes the weapon entity
entity notify( "damage", amount, attacker, direction_vec, point, type, tagName, ModelName, Partname, weapon ); //used when an entity takes damage. Passes a whole bunch of useful arguments. Note that you can use this on more than just players/zombies, so long as you use the function SetCanDamage() on the entity first.

trigger notify("trigger", player); //used when a player activates a trigger. Also passes the player who activated it as an argument. VERY USEFUL{% endhighlight %}
 <br />There are definitely more Treyarch notifies than these that you can use, you&#39;ll just have to look for them.<br /> <br /><span style="color:#00ffff;">WAITTILLS</span><br /> <br />Waittills are exactly how they sound: they wait until a notify happens before they allow the code below them to continue.<br /> <br />for example:<br /> <br />{% highlight php %}
steve waittill("stop_eating_my_sandwich");
IPrintLnBold("oh, sorry");{% endhighlight %}
 <br /> <br />waittills can also accept those useful arguments you may have passed in the notify:<br /> <br />{% highlight php %}
level waittill("magic_door_opened", entity, vector);
entity MoveTo(entity.origin + vector, 5);{% endhighlight %}
 <br /> <br />but perhaps one of the most used waittills is for waiting until a use_trigger is triggered:<br /> <br />{% highlight php %}
trig = GetEnt("button_trig","targetname");
trig SetHintString( "Press and Hold ^3[{+activate}]^7 to Press Button [Cost: 500 ]");
trig.activated = false;
while(trig.activated == false)
{
    trig waittill("trigger", player);
    if(player.score &gt;= 500)
    {
        player zm_score::minus_to_player_score( 500 );
        trig.activated = true;
    }
    else
    {
        IPrintLnBold("You need some more cash");
    }
}
IPrintLnBold("Button Activated");{% endhighlight %}
 <br /> <br />The code above is a good example of how while loops and waittills can work together, in this case in what is known as a purchase loop.<br /> <br /><span style="color:#00ffff;">ENDONS</span><br /> <br /><span style="color:#ffcc00;">Endons </span>will basically break the code below them once the notify they are waiting for is passed. This is very useful for getting out of while loops.<br /> <br />{% highlight php %}
level endon("intermission");
player endon("death");
while(1)
{
    if(player IsTouching(trigger))
    {
        player zm_score::add_to_player_score( 10 );
        wait(0.95);
    }
    wait(0.05);
}{% endhighlight %}
 <br /> <br />In the example above, I have two different endons, placed <strong>above</strong> my while loop. for the first one, the while loop will break when game ends, and the level variable is given the &quot;intermission&quot; notify. For the second one, if the player is given the notify &quot;death&quot; (which should automatically happen when a player dies), the while loop will also break. <br /> <br />If either of these notifies happen, the while loop will &quot;end&quot;.<br /> <br /> <br /><a href="https://forum.modme.co/threads/abnormal202-scripting-tutorials-master.2680/">See ALL Tutorials</a></p>
