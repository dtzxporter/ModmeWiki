# Perk machines that requires Power from a Gun
Game Modding | Call of Duty: Black Ops 3 | Scripting

---
<strong style="font-size: 1.4em;"><span style="text-decoration: underline;text-decoration-color: #34a7f9;"><span style="color:#34a7f9;">ModmeBot</span></span>:</strong>

<p>Thread By: Yasser143<br />I would like to know if their is a way to make the perk machines (Not Including Pap) requires the power on and a shot of the wunderwaffe dg-2 on a trigger that turns on the perk machines ( Each Perk machine has its own trigger, each needs to get Shot once from the wunderwaffe).</p>

---
<strong style="font-size: 1.4em;"><span style="text-decoration: underline;text-decoration-color: #34a7f9;"><span style="color:#34a7f9;">ModmeBot</span></span>:</strong>

<p>Reply By: Ping998<br /><blockquote><em>Yasser143</em>I would like to know if their is a way to make the perk machines (Not Including Pap) requires the power on and a shot of the wunderwaffe dg-2 on a trigger that turns on the perk machines ( Each Perk machine has its own trigger, each needs to get Shot once from the wunderwaffe). </blockquote><br /><br /><p style="text-align:left;"></p><p style="text-align:left;">You could use a trigger_damage and make it weapon specific (I don&#39;t know how to do that in BO3 but it was possible in WaW so I assume it still is).</p><p style="text-align:left;"></p><p style="text-align:left;">Then you could link it to a door (using target/targetname) which would open up to a perk machine or something like that.</p><p style="text-align:left;"></p><p style="text-align:left;">Hope this helps!</p></p>

---
<strong style="font-size: 1.4em;"><span style="text-decoration: underline;text-decoration-color: #34a7f9;"><span style="color:#34a7f9;">ModmeBot</span></span>:</strong>

<p>Reply By: natesmithzombies<br /><p style="text-align:left;">Untested (not on a proper PC atm) but here is something to get you started. Let me know if it works out. </p><p style="text-align:left;"></p><p style="text-align:left;">1) Add this to the bottom of your mapname.gsc: </p>{% highlight php %}
function triggered_perks()
{
	trigs = GetEntArray( "triggered_perks", "targetname" ); 
	foreach( trig in trigs )
		trig thread damage_think(); 
	
	thread powered_perk_override(); 
}

function powered_perk_override()
{
	level flag::wait_till( "start_zombie_round_logic" );
	wait(0.1); 
	
	vending_triggers = GetEntArray( "zombie_vending", "targetname" );
	foreach ( trigger in vending_triggers )
	{
		powered_on = zm_perks::get_perk_machine_start_state(trigger.script_noteworthy);
		powered_perk = zm_power::add_powered_item( undefined, undefined, undefined, undefined, undefined, undefined, trigger );
		zm_power::remove_powered_item( powered_perk ); 
	}
}

function damage_think()
{
	level endon( "intermission" ); 
	
	while(1)
	{
		self waittill( "damage", amount, attacker, direction_vec, point, type, tagName, modelName, partName, weapon, dFlags, inflictor, chargeLevel );
		if( weapon == GetWeapon("tesla_gun") ) // change tesla_gun to whatever weapon you want
		{
			str_on = self.script_noteworthy + "_on";
			level notify( str_on ); 
			break; 
		}
	}
}
{% endhighlight %}
<br /><br /><br /><p style="text-align:left;">2) Add this to your function main of your mapname.gsc: </p>{% highlight php %}
triggered_perks();
{% endhighlight %}
<br /><br /><br /><p style="text-align:left;">3) Insert a trigger damage with the following KVPS in your map: </p><p style="text-align:left;">targetname - triggered_perks</p><p style="text-align:left;">script_noteworthy - perk_specific*</p><p style="text-align:left;"></p><p style="text-align:left;">*These are your options for the script_noteworthy: </p>&lt;ul style=&quot;padding-left:40px;margin:0;&quot;&gt;<br />&lt;li style=&quot;width:100%;&quot;&gt;juggernog&lt;/li&gt;&lt;li style=&quot;width:100%;&quot;&gt;widows_wine&lt;/li&gt;&lt;li style=&quot;width:100%;&quot;&gt;doubletap&lt;/li&gt;&lt;li style=&quot;width:100%;&quot;&gt;electric_cherry&lt;/li&gt;&lt;li style=&quot;width:100%;&quot;&gt;revive&lt;/li&gt;&lt;li style=&quot;width:100%;&quot;&gt;marathon&lt;/li&gt;&lt;li style=&quot;width:100%;&quot;&gt;sleight&lt;/li&gt;&lt;/ul&gt;<br /><br /><p style="text-align:left;">4) Compile and report back after testing </p><p style="text-align:left;"></p><p style="text-align:left;">5) If you get an error saying something about unrecognized function add this to your mapname.gsc at the very top: </p>{% highlight php %}
#using scripts\zm\_zm_power;
{% endhighlight %}
<br /><br /><br /><p style="text-align:left;"></p><p style="text-align:left;"></p></p>

---
<strong style="font-size: 1.4em;"><span style="text-decoration: underline;text-decoration-color: #34a7f9;"><span style="color:#34a7f9;">ModmeBot</span></span>:</strong>

<p>Reply By: Yasser143<br /><p style="text-align:left;">It works, but I dont want the power switch to also turn on the perks as well, only the weapon should be able to. But besides that it works.</p></p>

---
<strong style="font-size: 1.4em;"><span style="text-decoration: underline;text-decoration-color: #34a7f9;"><span style="color:#34a7f9;">ModmeBot</span></span>:</strong>

<p>Reply By: natesmithzombies<br /><blockquote><em>Yasser143</em><p style="text-align:left;">It works, but I dont want the power switch to also turn on the perks as well, only the weapon should be able to. But besides that it works.</p></blockquote><p style="text-align:left;">I updated my original code to try and counter this. Reinstall and let me know if it works. I also had to add an additional step 5 in case the _zm_power.gsc is not already used in the mapaname.gsc. If you dont get the error let me know so I can remove that step if it is redundant </p></p>

---
<strong style="font-size: 1.4em;"><span style="text-decoration: underline;text-decoration-color: #34a7f9;"><span style="color:#34a7f9;">ModmeBot</span></span>:</strong>

<p>Reply By: Yasser143<br /><blockquote><em>natesmithzombies</em><blockquote><em>Yasser143</em><p style="text-align:left;">It works, but I dont want the power switch to also turn on the perks as well, only the weapon should be able to. But besides that it works.</p></blockquote><p style="text-align:left;">I updated my original code to try and counter this. Reinstall and let me know if it works. I also had to add an additional step 5 in case the _zm_power.gsc is not already used in the mapaname.gsc. If you dont get the error let me know so I can remove that step if it is redundant </p></blockquote><p style="text-align:left;">Even with the addition support of step 5, im getting this error from the script, </p><p style="text-align:left;">Unresolved external &#39;zm_perks::get_perk_machine_start_state</p></p>

---
<strong style="font-size: 1.4em;"><span style="text-decoration: underline;text-decoration-color: #34a7f9;"><span style="color:#34a7f9;">ModmeBot</span></span>:</strong>

<p>Reply By: natesmithzombies<br /><blockquote><em>Yasser143</em><blockquote><em>natesmithzombies</em><blockquote><em>Yasser143</em><p style="text-align:left;">It works, but I dont want the power switch to also turn on the perks as well, only the weapon should be able to. But besides that it works.</p></blockquote><p style="text-align:left;">I updated my original code to try and counter this. Reinstall and let me know if it works. I also had to add an additional step 5 in case the _zm_power.gsc is not already used in the mapaname.gsc. If you dont get the error let me know so I can remove that step if it is redundant </p></blockquote><p style="text-align:left;">Even with the addition support of step 5, im getting this error from the script, </p><p style="text-align:left;">Unresolved external &#39;zm_perks::get_perk_machine_start_state</p></blockquote><p style="text-align:left;">Interesting it appears that this must be missing in your mapname.gsc as well: </p>{% highlight php %}
#using scripts\zm\_zm_perks;
{% endhighlight %}
<br /><br /><br /><p style="text-align:left;">insert that at the top of the script and see if it compiles. Sorry I am not on a proper computer to test myself atm</p></p>

---
<strong style="font-size: 1.4em;"><span style="text-decoration: underline;text-decoration-color: #34a7f9;"><span style="color:#34a7f9;">ModmeBot</span></span>:</strong>

<p>Reply By: Yasser143<br /><p style="text-align:left;">Ya its still is the same as the last script, the dg2 turns on the perks, but the power switch also turns on the perks as well.</p></p>

---
<strong style="font-size: 1.4em;"><span style="text-decoration: underline;text-decoration-color: #34a7f9;"><span style="color:#34a7f9;">ModmeBot</span></span>:</strong>

<p>Reply By: natesmithzombies<br /><blockquote><em>Yasser143</em><p style="text-align:left;">Ya its still is the same as the last script, the dg2 turns on the perks, but the power switch also turns on the perks as well.</p></blockquote><p style="text-align:left;">Damn sorry to put this one on hold, but I will revisit this topic after I finish Brutus. I am on a limited time crunch and this one has to be tested on my home PC</p></p>

---
<strong style="font-size: 1.4em;"><span style="text-decoration: underline;text-decoration-color: #34a7f9;"><span style="color:#34a7f9;">ModmeBot</span></span>:</strong>

<p>Reply By: Yasser143<br /><p style="text-align:left;">No Problem Nate, just let me know when u have time to finish up this script for me. Thx</p></p>

---
<strong style="font-size: 1.4em;"><span style="text-decoration: underline;text-decoration-color: #34a7f9;"><span style="color:#34a7f9;">ModmeBot</span></span>:</strong>

<p>Reply By: Yasser143<br /><p style="text-align:left;">Nate do u have any time on hand to finish up the script?</p></p>

---
<strong style="font-size: 1.4em;"><span style="text-decoration: underline;text-decoration-color: #34a7f9;"><span style="color:#34a7f9;">ModmeBot</span></span>:</strong>

<p>Reply By: natesmithzombies<br /><p style="text-align:left;">I have tested this and can assure you that it works: </p><p style="text-align:left;"></p><p style="text-align:left;">Step 1) Paste this at the bottom of your mapname.gsc</p>{% highlight php %}
function triggered_perks()
{
	zm_perks::register_perk_machine_power_override( "specialty_armorvest", &amp;wait_for_trigger ); 
	zm_perks::register_perk_machine_power_override( "specialty_quickrevive", &amp;wait_for_trigger ); 
	zm_perks::register_perk_machine_power_override( "specialty_fastreload", &amp;wait_for_trigger ); 
	zm_perks::register_perk_machine_power_override( "specialty_doubletap2", &amp;wait_for_trigger ); 
	zm_perks::register_perk_machine_power_override( "specialty_staminup", &amp;wait_for_trigger ); 
	zm_perks::register_perk_machine_power_override( "specialty_phdflopper", &amp;wait_for_trigger ); 
	zm_perks::register_perk_machine_power_override( "specialty_deadshot", &amp;wait_for_trigger ); 
	zm_perks::register_perk_machine_power_override( "specialty_additionalprimaryweapon", &amp;wait_for_trigger ); 
	zm_perks::register_perk_machine_power_override( "specialty_electriccherry", &amp;wait_for_trigger ); 
	zm_perks::register_perk_machine_power_override( "specialty_widowswine", &amp;wait_for_trigger ); 
}

function wait_for_trigger()
{
	if( isDefined( level.run_special_power_once ) ) // make the func run once and only once
		return;
	level.run_special_power_once = true; 
	
	trigs = GetEntArray( "triggered_perks", "targetname" ); 
	foreach( trig in trigs )
		trig thread activate_on_shoot(); 
}

function activate_on_shoot()
{
	while(1)
	{
		self waittill( "damage", amount, attacker, direction_vec, point, type, tagName, modelName, partName, weapon, dFlags, inflictor, chargeLevel );
		if( weapon == GetWeapon("tesla_gun") ) // change tesla_gun to whatever weapon you want
		{
			perk_machine_think( self.script_noteworthy, level._custom_perks[ self.script_noteworthy ] );
			break; 
		}
	}
}

function perk_machine_think( str_key, s_custom_perk )
{	
	str_endon = str_key + PERK_END_POWER_THREAD;
	level endon( str_endon );//Support endon for any perk machine
	
	str_on = s_custom_perk.alias + "_on";
	str_off = s_custom_perk.alias + "_off";
	str_notify = str_key + "_power_on";
	
	while ( true )
	{
		machine = getentarray( s_custom_perk.radiant_machine_name, "targetname");
		machine_triggers = GetEntArray( s_custom_perk.radiant_machine_name, "target" );
		
		for( i = 0; i &lt; machine.size; i++ )
			machine[i] SetModel(level.machine_assets[str_key].off_model);
		
		level thread zm_perks::do_initial_power_off_callback( machine, str_key );
		array::thread_all( machine_triggers, &amp;zm_perks::set_power_on, false );
	
		// level waittill( str_on );
	
		for( i = 0; i &lt; machine.size; i++ )
		{
			machine[i] SetModel(level.machine_assets[str_key].on_model);
			machine[i] vibrate((0,-100,0), 0.3, 0.4, 3);
			machine[i] playsound("zmb_perks_power_on");
			machine[i] thread zm_perks::perk_fx( s_custom_perk.machine_light_effect );
			machine[i] thread zm_perks::play_loop_on_machine();
		}
		level notify( str_notify );
		
		array::thread_all( machine_triggers, &amp;zm_perks::set_power_on, true );
		if( isdefined( level.machine_assets[ str_key ].power_on_callback ) )
		{
			array::thread_all( machine, level.machine_assets[ str_key ].power_on_callback );
		}
		
		level waittill( str_off );
			
		if( isdefined( level.machine_assets[ str_key ].power_off_callback ) )
		{
			array::thread_all( machine, level.machine_assets[ str_key ].power_off_callback );
		}
		array::thread_all( machine, &amp;zm_perks::turn_perk_off );
	}
}
{% endhighlight %}
<br /><br /><br /><p style="text-align:left;"></p><p style="text-align:left;">Step 2) Add this to the top of your mapname.gsc: </p>{% highlight php %}
#insert scripts\zm\_zm_perks.gsh;
{% endhighlight %}
<br /><br /><br /><p style="text-align:left;"></p><p style="text-align:left;">Step 3) Add this to function main in mapname.gsc</p>{% highlight php %}
level thread triggered_perks();
{% endhighlight %}
<br /><br /><br /><p style="text-align:left;"></p><p style="text-align:left;">Sep 4) Add a trigger&gt;damage for each perk you would like and give it the following KVPs: </p><p style="text-align:left;">targetname - triggered_perks</p><p style="text-align:left;">script_noteworthy - specialty_name*</p><p style="text-align:left;"></p><p style="text-align:left;">* where specialty name is specialty_armorvest, specialty_quickrevive, etc. </p><p style="text-align:left;"></p><p style="text-align:left;"></p></p>

---
<strong style="font-size: 1.4em;"><span style="text-decoration: underline;text-decoration-color: #34a7f9;"><span style="color:#34a7f9;">ModmeBot</span></span>:</strong>

<p>Reply By: Yasser143<br /><p style="text-align:left;">I get this error when I link it, &#39;zm_perks::register_perk_machine_power_override&#39;</p></p>

---
<strong style="font-size: 1.4em;"><span style="text-decoration: underline;text-decoration-color: #34a7f9;"><span style="color:#34a7f9;">ModmeBot</span></span>:</strong>

<p>Reply By: natesmithzombies<br /><blockquote><em>Yasser143</em><p style="text-align:left;">I get this error when I link it, &#39;zm_perks::register_perk_machine_power_override&#39;</p></blockquote><p style="text-align:left;">Add these to the top of your mapname.gsc</p>{% highlight php %}
#using scripts\zm\_zm_perks;
#insert scripts\zm\_zm_perks.gsh;
{% endhighlight %}
</p>
